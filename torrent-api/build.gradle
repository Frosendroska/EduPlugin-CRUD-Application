buildscript {
    ext {
        protobufVersion = '3.19.1'
        grpcVersion = '1.42.1'
    }
}

plugins {
    id 'com.google.protobuf'
}

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation 'io.grpc:grpc-stub:1.40.1'
    implementation "com.google.protobuf:protobuf-java:$protobufVersion"

    compileOnly "jakarta.annotation:jakarta.annotation-api:1.3.5" // Java 9+ compatibility - Do NOT update to 2.0.0
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }

    generateProtoTasks {
        all()*.plugins {
            java {}
            grpc {}
        }
    }

    generatedFilesBaseDir = "$projectDir/src/generated"
    clean {
        delete generatedFilesBaseDir
    }
}

idea {
    module {
        generatedSourceDirs += file('src/generated')
    }
}
